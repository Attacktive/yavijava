apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'signing'

group = "com.toastcoders"
archivesBaseName = "yavijava"
version = '5.5.07'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.apache.directory.studio:org.dom4j.dom4j:1.6.1'
    compile 'log4j:log4j:1.2.17'
    compile 'org.apache.httpcomponents:httpclient:4.3.5'
    testCompile 'javax.servlet:javax.servlet-api:3.1.0'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'junit:junit:4.12-beta-3'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'cglib:cglib-nodep:3.1'
}

sourceSets {
    main {
        java {
        }
    }
}

test {
    testLogging {
        events 'started', 'passed'
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'yavijava'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Java library for VMware vSphere'
                url 'https://github.com/michaelrice/yavijava'

                scm {
                    connection 'scm:git:https://github.com/michaelrice/yavijava.git'
                    developerConnection 'scm:git:https://github.com/michaelrice/yavijava.git'
                    url 'https://github.com/michaelrice/yavijava'
                }

                licenses {
                    license {
                        name 'BSD'
                        url 'http://opensource.org/licenses/BSD-3-Clause'
                    }
                }

                developers {
                    developer {
                        id 'michaelrice'
                        name 'Michael Rice'
                        email 'michael@michaelrice.org'
                    }
                }
            }
        }
    }
}